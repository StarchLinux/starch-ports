# $Id: PKGBUILD 153633 2012-03-16 22:34:33Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=udev
pkgver=181
pkgrel=5
pkgdesc="The userspace dev tools (udev)"
depends=('util-linux' 'libusb-compat' 'kmod' 'pciutils' 'usbutils')
install=udev.install
arch=(i686 x86_64)
license=('GPL')
makedepends=('gperf' 'usbutils' 'kmod')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/$pkgname-$pkgver.tar.xz
	no_ts_usec.diff
	0001-reinstate-TIMEOUT-handling.patch
	initcpio-hooks-udev
	initcpio-install-udev)
url="http://git.kernel.org/?p=linux/hotplug/udev.git;a=summary"
backup=(etc/udev/udev.conf)
groups=('base')
options=(!makeflags !libtool)

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p 1 < ../0001-reinstate-TIMEOUT-handling.patch
  patch -p 1 < ../no_ts_usec.diff

  ./configure --prefix=/usr \
              --with-rootprefix= \
              --sysconfdir=/etc \
              --libdir=/usr/lib \
              --libexecdir=/lib \
              --enable-static \
              --disable-shared \
              --disable-gtk-doc{,-{html,pdf}} \
              --disable-gudev \
              --disable-introspection \
              --disable-udev_acl

  echo Makefile | situ 'sed ''s/-Wtype-limits//'''

  make
}
  
package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install

  # install the mkinitpcio hook
  install -D -m644 ../initcpio-hooks-udev ${pkgdir}/usr/lib/initcpio/hooks/udev
  install -D -m644 ../initcpio-install-udev ${pkgdir}/usr/lib/initcpio/install/udev

  # udevd moved, symlink to make life easy for restarting udevd manually
  ln -s ../../lib/udev/udevd ${pkgdir}/usr/bin/udevd

  # the path to udevadm is hardcoded in some places
  install -d ${pkgdir}/sbin
  ln -s ../usr/bin/udevadm ${pkgdir}/sbin/udevadm

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for x in $pkgdir/lib/udev/rules.d/*.rules; do echo $x; done | \
    situ \
    sed\ \''s#GROUP="dialout"#GROUP="uucp"#g;
            s#GROUP="tape"#GROUP="storage"#g;
            s#GROUP="cdrom"#GROUP="optical"#g'\'
}

sha1sums=('75c17faeafc13f0e7ba7b7e1d233f602945734bb'
          'f2c0c9a65e056a88361395087a2beb291f46c898'
          '6239f39a74f215c50deac9d0ac1932608c4352ea'
          '02ac41710e182d6d575a6950f14170e75e5d18e3'
          '52622817eb0a097f14a371f207326e54241a99e7')
