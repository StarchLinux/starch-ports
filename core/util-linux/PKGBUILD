# $Id: PKGBUILD 163535 2012-07-15 00:25:36Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=util-linux
pkgver=2.21.2
pkgrel=5
pkgdesc="Miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
groups=('base')
depends=('pam')
conflicts=('util-linux-ng')
provides=("util-linux-ng=${pkgver}")
license=('GPL2')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${pkgname}/v2.21/${pkgname}-${pkgver}.tar.xz mountpoint)
backup=(etc/pam.d/chfn etc/pam.d/chsh etc/pam.d/login)
install=util-linux.install

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-fs-paths-extra=/usr/bin:/usr/sbin \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --disable-new-mount \
              --disable-login-utils \
              --with-ncurses=no \
              --disable-shared

  make LIBS='-lcrypt -lutil'
}

check() {
  #make -C "$pkgname-$pkgver" check
  true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  # broken tool, going away in next major release, so just remove it now
  rm "${pkgdir}"/usr/{bin/chkdupexe,share/man/man1/chkdupexe.1}

  # delete stray empty dir, fixed upstream
  rm -r usr/share/man/ru

  test -e bin/mountpoint || cp $srcdir/mountpoint bin/ && chmod +x bin/mountpoint
}

sha1sums=('f1ebeb8fe32f11f79bbc5d26e89cc801d1663d2f'
          '11efa948fdf98dadc5b0ce234a2b9c21c7bc0f3d')
