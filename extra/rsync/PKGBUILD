# $Id: PKGBUILD 165402 2012-08-18 01:22:52Z heftig $

pkgname=rsync
pkgver=3.0.9
pkgrel=4
pkgdesc="A file transfer program to keep remote files in sync"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=()
backup=('etc/rsyncd.conf')
source=("http://rsync.samba.org/ftp/rsync/$pkgname-$pkgver.tar.gz"
        'rsyncd.conf' 'rsyncd')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --with-included-popt
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	#make test
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm755 ../rsyncd "$pkgdir/etc/rc.d/rsyncd"
	install -Dm644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -Dm755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}

sha1sums=('c64c8341984aea647506eb504496999fd968ddfc'
          'cd71423da706ab726b1d004f0771742c183e5b3b'
          'b6b572c159084999ac66a03c8565fb734d2214b7')
